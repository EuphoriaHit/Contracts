// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0 <0.9.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

//This smart contract's reward distribution algorithm was based on this article
//https://uploads-ssl.webflow.com/5ad71ffeb79acc67c8bcdaba/5ad8d1193a40977462982470_scalable-reward-distribution-paper.pdf
    
contract Staking is Ownable{
    using SafeERC20 for IERC20;
    uint64 constant PRECISION = 1000000000000;

    modifier contractExpired() {
        uint256 currentDay = _getCurrentDay();
        if(_totalStakes == 0) {
            _daysInPause += currentDay - _lastActiveDay;
            _lastActiveDay = currentDay;
        }
        require(currentDay - _daysInPause >= _contractDurationInDays && _totalStakes == 0, "Staking: The staking contract is not yet expired");
        _;
    }

    modifier contractNotExpired() {
        uint256 currentDay = _getCurrentDay();
        if(_totalStakes == 0) {
            _daysInPause += currentDay - _lastActiveDay;
            _lastActiveDay = currentDay;
        }
        require(currentDay - _daysInPause < _contractDurationInDays, "Staking: The staking contract has already expired");
        _;
    }

    modifier contractStarted() {
        require(_stakingStarted == true, "Staking: The staking contract has not started yet");
        _;
    }

    modifier contractNotStarted() {
        require(_stakingStarted == false, "Staking: The staking contract has already started");
        _;
    }

    // <================================ CONSTRUCTOR AND INITIALIZER ================================>

    constructor(uint256 supplyPercentage, uint16 durationInDays, address tokenAddress) {
        _setToken(tokenAddress);
        _setSupplyAndDuration(supplyPercentage, durationInDays);
    }

    function changeSupplyAndDuration(uint256 supplyPercentage, uint16 durationInDays) external onlyOwner contractNotStarted {
        _setSupplyAndDuration(supplyPercentage, durationInDays);
    }

    function changeToken(address newTokenAddress) external onlyOwner contractNotStarted{
        _setToken(newTokenAddress);
    }

    function initialize()
        external
        onlyOwner
        contractNotStarted
    {
        require(_stakingStarted == false, "Staking: The staking contract has been already initialized");
        _stakingStarted = true;
        _startDate = block.timestamp - (block.timestamp % 1 days );
        transferTokensToContract(_initialSupply);
    }

    // <================================ END OF CONSTRUCTOR AND INITIALIZER ================================>
    // VARIABLES IN BYTES16 ARE USED FOR PRECISION CALCULATIONS WITH ABDKMATHQUAD LIBRARY

    IERC20 private _token;
    uint16 public _contractDurationInDays; // Duration of contract in days
    bool private _stakingStarted; // The boolean to check if the staking contract has been initialized and started the work process
    bool private _distributionEnded; // This boolean is used to control the work of distribuiteRewards() function
    mapping(address => mapping(uint256 => uint256)) private _distributedRewardsSnapshot; // S0 value in the Article Paper
    mapping(address => mapping(uint256 => uint256)) private _stake; // Keeps record of user's made stakings. Note that every new staking is considered as a seperate _stake transaction
    mapping(address => uint256) public _totalStakesAmount;
    mapping(address => uint256) public _lpTotalStakesAmount;
    mapping(address => uint256) private _stakesCount; // Total number of accomplished stakes by a specific user
    mapping(address => uint16) private _stakedDay;
    mapping(address => bool) private _isLiquidityProvider;
    uint256 private _distributedRewards; // S value in the Article Paper
    uint256 public _dailyReward; // Amount of tokens that will be distributed among all users in 1 Day 
    uint256 public _initialSupply; // Amount of tokens allocated for Staking contract use
    uint256 public _startDate; // Timestamp of the start day of the contract
    uint256 public _totalStakes; // Represents the total amount of staked Tokens. T value in the Article Paper. Find source of the article on top comment
    uint256 private _previousTotalStakes; // Represents the previous state of total amount of staked Tokens 
    uint256 private _lastActiveDay; // Represents the last day of last activity
    uint256 public _stakeHoldersCount; // Total number of stake holders (users)
    uint256 public _daysInPause; // Number of days with no active stakes. If there were no stakes on a specific day, then no reward is distributed and the duration of contract is extended to plus one day
    uint256 private _totalRewards; // The total amount of rewards that have already been distributed
    
    // <================================ EVENTS ================================>
    event StakeCreated(address indexed stakeHolder, uint256 indexed stake);

    event UnStaked(address indexed stakeHolder, uint256 indexed withdrawAmount);

    event TokensTransferedToStakingBalance(address indexed sender, uint256 indexed amount);

    // <================================ EXTERNAL FUNCTIONS ================================>

    // <<<================================= GETTERS =================================>>>

    function calculateReward() external contractStarted view returns (uint256) {
        address _stakeHolder = _msgSender();
        uint256 userStakesCount = _stakesCount[_stakeHolder];
        uint256 userStakesCountLP = _stakesCount[_stakeHolder];
        uint256 reward;
        uint256 distributedRewards = _distributedRewards;
        uint256 withdrawAmount;
        uint256 totalDeposited;
        uint256 totalDepositedLP;
        uint256 currentDay = _getCurrentDay();
        uint256 passedDays;
        require(userStakesCount != 0, "Staking: This user must be a stake holder");

        if (currentDay - _daysInPause > _contractDurationInDays) {
            passedDays = _contractDurationInDays - (_lastActiveDay - _daysInPause);
        } else {
            passedDays = currentDay - _lastActiveDay;
        }

        distributedRewards += (_dailyReward * passedDays * PRECISION) / _previousTotalStakes;

        // Calculation of User reward
        for(uint i = 0; i < userStakesCount; i++) {
            uint256 deposited = _stake[_stakeHolder][i];
            reward += (deposited * (distributedRewards - _distributedRewardsSnapshot[_stakeHolder][i])) / PRECISION;
            totalDeposited += deposited;
        }

        if(reward > 0) {
            withdrawAmount = _isLiquidityProvider[_stakeHolder] ? reward : reward + totalDeposited;
        } else {
            withdrawAmount = _isLiquidityProvider[_stakeHolder] ? 0 : totalDeposited;
        }

        return withdrawAmount;
    }

    function canUserUnstake(address stakeHolder) external view returns(bool) {
        return _getCurrentDay() > _stakedDay[stakeHolder];
    }

    function getUserStakesAmount(address stakeHolder) external view returns(uint256) {
        uint256 result;

        for(uint i = 0; i < _stakesCount[stakeHolder]; i++) {
            result += _stake[stakeHolder][i];
        }

        return result;
    }

    function getLiquidityProvider(address _stakeHolder)
        external
        view
    returns(bool) 
    {
        return _isLiquidityProvider[_stakeHolder];
    }

    // <<<================================= END OF GETTERS =================================>>>

    function transferTokensToContract(uint256 amount) public onlyOwner
    {
        address owner = _msgSender();
        _token.safeTransferFrom(owner, address(this), amount);
        emit TokensTransferedToStakingBalance(owner, amount);
    }

   function isStakeHolder(address stakeholder) public view returns(bool) {
        if(_stakesCount[stakeholder] != 0) {
            return true;
        }
       
       return false;
   }

    function createStake(uint256 stakeAmount)
       external
       contractStarted
       contractNotExpired
       returns (bool) 
    {
        address sender = _msgSender();
        _token.safeTransferFrom(sender, address(this), stakeAmount);
        require(_createStake(sender, stakeAmount, false), "Staking: Couldn't create stake");

        return true;
    }

    function createStakeLP(address stakeHolder, uint256 stakeAmount)
       external
       contractStarted
       contractNotExpired
       returns (bool) 
    {
        require(_createStake(stakeHolder, stakeAmount, true), "Staking: Couldn't create stake");
        _isLiquidityProvider[stakeHolder] = true;

        return true;
    }

    function unStake()
        external
        contractStarted
        returns (bool)
    {
        return _unStake(_msgSender(), false);
    }

    function unStakeLP(address stakeHolder)
        external
        contractStarted
        returns (bool)
    {
        require(_isLiquidityProvider[stakeHolder], "Staking: This user is not a liquidity provider");
        return _unStake(stakeHolder, true);
    }

    function finalize() external onlyOwner contractStarted contractExpired {
        address owner = _msgSender();
        uint256 balanceEUPH = _token.balanceOf(address(this));
        if(balanceEUPH > 0)  _token.safeTransfer(owner, balanceEUPH);
        selfdestruct(payable(owner));
    }

    // <================================ INTERNAL FUNCTIONS ================================>

    function decimals() internal pure returns(uint8) {
        return 3;
    }

    function toKiloToken(uint256 amount) internal pure returns(uint256) {
        return amount * (10 ** decimals());
    }

    function balanceOfContract()
       internal
       view
       returns(uint256)
   {
       return _token.balanceOf(address(this));
   }

    // <================================ PRIVATE FUNCTIONS ================================>

    function removeStakeHolder(address stakeholder) private contractStarted {
       require(stakeholder != address(0), "Staking: No zero address is allowed");
       bool _isStakeHolder = isStakeHolder(stakeholder);
       require(_isStakeHolder == true, "Staking: There is not any stake holder with provided address");

        for(uint i = 0; i < _stakesCount[stakeholder]; i++) {
            delete _stake[stakeholder][i];
            delete _distributedRewardsSnapshot[stakeholder][i];
        }
        delete _stakesCount[stakeholder];
        delete _lpTotalStakesAmount[stakeholder];
        delete _totalStakesAmount[stakeholder];
        
        delete _stakedDay[stakeholder];
        _stakeHoldersCount -= 1;
   }

   function _setSupplyAndDuration(uint256 supplyPercentage, uint16 durationInDays) private {
       require(durationInDays > 0, "Staking: Duration cannot be a zero value");
       require(supplyPercentage > 0 && supplyPercentage <= 100, "Staking: Supply percentage can be in a range between 0 and 100");
        _contractDurationInDays = durationInDays;
        _initialSupply = (_token.totalSupply() * supplyPercentage) / 100;
        _dailyReward = _initialSupply / _contractDurationInDays;
   }

   function _setToken(address newTokenAddress) private {
        require(
            address(_token) != newTokenAddress,
            "Staking: Cannot change token of same address"
        );
        _token = IERC20(newTokenAddress);
    }

    function _getCurrentDay() private view returns (uint256) 
    {
        return (block.timestamp - _startDate) / 1 days;    
    }

    function _distributeRewards()
        private
    {
        uint256 currentDay = _getCurrentDay();
        uint256 passedDays;

        if(_lastActiveDay == currentDay || _lastActiveDay == _contractDurationInDays + _daysInPause) return;
        
        if (currentDay - _daysInPause > _contractDurationInDays) {
            _distributionEnded = true;
            passedDays = _contractDurationInDays - (_lastActiveDay - _daysInPause);
        } else {
            passedDays = currentDay - _lastActiveDay;
        }

        _distributedRewards += (_dailyReward * passedDays * PRECISION) / _previousTotalStakes;

        _lastActiveDay = currentDay;
    }

    function _createStake(address stakeHolder, uint256 stakeAmount, bool isLP) private returns (bool) {
        require(stakeHolder != address(0), "Staking: No zero address is allowed");
        require(stakeAmount >= toKiloToken(10000), "Staking: Minimal stake value is 10 000 euphoria tokens");
        uint256 stakeId = _stakesCount[stakeHolder];
        uint256 currentDay = _getCurrentDay();

        if(!isStakeHolder(stakeHolder))
        { 
            _stakeHoldersCount += 1;
            _stakedDay[stakeHolder] = uint16(currentDay);
        }
        if(!_distributionEnded) {
            if(currentDay != _lastActiveDay)
            {
                _distributeRewards();
            }
        }

        _stake[stakeHolder][stakeId] = stakeAmount;
        _distributedRewardsSnapshot[stakeHolder][stakeId] = _distributedRewards;
        _stakesCount[stakeHolder] += 1;
        
        if(isLP){
            _lpTotalStakesAmount[stakeHolder] += stakeAmount;
        } else {
            _totalStakesAmount[stakeHolder] += stakeAmount;
        }
        
        _totalStakes += stakeAmount;

        if(_previousTotalStakes == 0 || currentDay == _lastActiveDay) {
            _previousTotalStakes = _totalStakes;
        }

        emit StakeCreated(stakeHolder, stakeAmount);

        return true;
   }

   function _unStake(address stakeHolder, bool isLP) private returns (bool) {
        require(stakeHolder != address(0), "Staking: Zero address is prohibited");
        require(isStakeHolder(stakeHolder), "Staking: There is not any stake holder with provided address");
        uint256 userStakesCount = _stakesCount[stakeHolder];
        uint256 reward;
        uint256 withdrawAmount;
        uint256 totalDeposited;
        uint256 totalStakesAmount = _totalStakesAmount[stakeHolder];
        uint256 lpTotalStakesAmount = _lpTotalStakesAmount[stakeHolder];

        if(!_distributionEnded)
        {
            if(_getCurrentDay() != _lastActiveDay) {
                _distributeRewards();
            }
        }

        // Calculation of User reward
        for(uint i = 0; i < userStakesCount; i++) {
            uint256 deposited = _stake[stakeHolder][i];
            reward += (deposited * (_distributedRewards - _distributedRewardsSnapshot[stakeHolder][i])) / PRECISION;
            totalDeposited += deposited;
        }

        _totalStakes -= isLP ? lpTotalStakesAmount : totalStakesAmount;
        _previousTotalStakes = _totalStakes;

        if(reward > 0) {
            _totalRewards += reward;
            withdrawAmount = isLP ? reward : reward + totalDeposited;
        } else {
            withdrawAmount = isLP ? 0 : totalDeposited;
        }

        if(withdrawAmount != 0) { _token.safeTransfer(stakeHolder, withdrawAmount); }
        removeStakeHolder(stakeHolder);
        
        if(totalDeposited - totalStakesAmount != 0 || totalDeposited - lpTotalStakesAmount != 0)
        {
            if(totalDeposited - totalStakesAmount != 0 && totalDeposited - lpTotalStakesAmount == 0)
            {
                _stake[stakeHolder][0] = totalDeposited - totalStakesAmount;
                _lpTotalStakesAmount[stakeHolder] = totalDeposited - totalStakesAmount;
            } else {
                _stake[stakeHolder][0] = totalDeposited - lpTotalStakesAmount;
                _totalStakesAmount[stakeHolder] = totalDeposited - lpTotalStakesAmount;
            }
            
            _distributedRewardsSnapshot[stakeHolder][0] = _distributedRewards;
            _stakesCount[stakeHolder] = 1;
            _stakeHoldersCount += 1;
        }

        emit UnStaked(stakeHolder, withdrawAmount);

        return true;
    }
}